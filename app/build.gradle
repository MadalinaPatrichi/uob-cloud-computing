buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'uob-todo-app'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

node {
    version = '10.9.0'
    npmVersion = '6.2.0'
    download = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-freemarker")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.session:spring-session-jdbc")
    compile("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.0.0.RELEASE")
    compile("org.bouncycastle:bcprov-jdk15on:1.60")
    compile("org.apache.httpcomponents:httpclient")
    compile("org.hsqldb:hsqldb")
    compile("mysql:mysql-connector-java")
    compile("javax.xml.bind:jaxb-api")
    compile("io.micrometer:micrometer-registry-prometheus")
    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.files(fileTree("src/main/js"))
    def osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("windows")) {
        script = project.file('node_modules/webpack/bin/webpack.js')
    } else {
        script = project.file('node_modules/.bin/webpack')
    }
}

task setHttpProxyFromEnv {
    def map = ['HTTP_PROXY': 'http', 'HTTPS_PROXY': 'https']
    for (e in System.getenv()) {
        def key = e.key.toUpperCase()
        if (key in map) {
            def base = map[key]
            def url = e.value.toURL()
            println " - systemProp.${base}.proxy=${url.host}:${url.port}"
            System.setProperty("${base}.proxyHost", url.host.toString())
            System.setProperty("${base}.proxyPort", url.port.toString())
        }
    }
}

build.dependsOn setHttpProxyFromEnv
processResources.dependsOn webpack

clean.delete << file('node_modules')
clean.delete << file('src/main/resources/static')
